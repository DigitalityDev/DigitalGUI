plugins {
    id "java-library"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = "dev.digitality"
version = "1.0.1"
description = "DigitalGUI"

java.sourceCompatibility = JavaLanguageVersion.of(8)
java.targetCompatibility = JavaLanguageVersion.of(8)
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()

    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }

    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    maven {
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
    implementation "de.tr7zw:item-nbt-api:2.11.3"

    compileOnly "org.jetbrains:annotations:24.0.1"
    compileOnly "org.projectlombok:lombok:1.18.22"
    annotationProcessor "org.projectlombok:lombok:1.18.22"
}

shadowJar {
    minimize()

    relocate "de.tr7zw.changeme.nbtapi", "dev.digitality.digitalgui.nbtapi"

    archiveClassifier = null
}

publishing {
    repositories {
        maven {
            name "digitalityRepoReleases"
            url "https://repo.digitality.dev/releases"

            credentials {
                username project.findProperty("digitalityRepo.username") ?: System.getenv("DIGITALITY_REPO_USERNAME")
                password project.findProperty("digitalityRepo.password") ?: System.getenv("DIGITALITY_REPO_PASSWORD")
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) { publication ->
            groupId = this.group
            artifactId = this.name.toLowerCase()
            version = this.version

            from(project.shadow.component(publication))
        }
    }
}